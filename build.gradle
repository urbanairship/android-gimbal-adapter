// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        adapterVersion = '7.2.0'

        // Dependencies
        airshipVersion = '14.6.0'
        gimbalVersion = '4.5.2'

        // Android SDK versions
        compileSdkVersion = 29
        targetSdkVersion = 29
        minSdkVersion = 21

        // Gradle plugins
        gradlePluginVersion = '4.1.3'
        nexusPublishPluginVersion = '1.1.0'
    }

    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version "$nexusPublishPluginVersion"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task getVersion() {
    doLast {
        println adapterVersion
    }
}

nexusPublishing {
    // user and pass are required for publishing.
    def user = System.getenv("SONATYPE_USERNAME") ?: findProperty("sonatype.username")
    def pass = System.getenv("SONATYPE_PASSWORD") ?: findProperty("sonatype.password")
    // profileId is optional, but supposedly speeds things up if set.
    def profileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: findProperty("sonatype.stagingProfileId")
    if (user != null && pass != null) {
        repositories {
            sonatype {
                username.set(user)
                password.set(pass)
                stagingProfileId.set(profileId)
            }
        }
    } else {
        logger.info("Missing publishing credentials! Nexus publishing will not be configured.")
    }
}

