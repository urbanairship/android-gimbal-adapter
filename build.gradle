// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        adapterVersion = '7.5.0'

        // Dependencies
        airshipVersion = '16.7.2'
        gimbalVersion = '4.8.0'

        // Android SDK versions
        compileSdkVersion = 32
        targetSdkVersion = 32
        minSdkVersion = 21

        // Gradle plugins
        gradlePluginVersion = '7.3.0'
        nexusPublishPluginVersion = '1.1.0'
    }

    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version "$nexusPublishPluginVersion"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task getVersion() {
    doLast {
        println adapterVersion
    }
}

nexusPublishing {
    // user and pass are required for publishing.
    def user = System.getenv("SONATYPE_USERNAME") ?: findProperty("sonatype.username")
    def pass = System.getenv("SONATYPE_PASSWORD") ?: findProperty("sonatype.password")
    // profileId is optional, but supposedly speeds things up if set.
    def profileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: findProperty("sonatype.stagingProfileId")
    if (user != null && pass != null) {
        repositories {
            sonatype {
                username.set(user)
                password.set(pass)
                stagingProfileId.set(profileId)
            }
        }
    } else {
        logger.info("Missing publishing credentials! Nexus publishing will not be configured.")
    }
}

