group = 'com.urbanairship.android'
version = '2.0.0'
description = "Urban Airship Gimbal Adapter"

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation group:'com.gimbal.android.v3', name:'gimbal-sdk', version:'3.0'
    implementation group:'com.gimbal.android.v3', name:'gimbal-slf4j-impl', version:'3.0'
    implementation 'com.urbanairship.android:urbanairship-sdk:8.9.6'
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId project.group
            artifactId 'gimbal-adapter'

            name project.name
            description project.description
            url 'http://www.urbanairship.com'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        userOrg = 'urbanairship'
        name = project.name
        desc = project.description
        websiteUrl = 'http://www.urbanairship.com'

        version {
            repo = 'android'
            publish = false
            desc = project.description
            name = project.version
            released = new Date()
        }
    }
}