group = 'com.gimbal.android.v4'
description = "Urban Airship Gimbal Adapter"
version = rootProject.adapterVersion

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion as Integer
        targetSdkVersion rootProject.targetSdkVersion as Integer
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compileOnly "com.gimbal.android.v4:gimbal-sdk:4.7.0"
    compileOnly "com.gimbal.android.v4:gimbal-slf4j-impl:4.7.0"
    api "com.urbanairship.android:urbanairship-core:16.5.0"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets["main"].java.srcDirs
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            name 'Gimbal Developers'
            email 'developers@gimbal.com'
            organization 'Gimbal, Inc.'
            organizationUrl 'https://gimbal.com'
        }
    }

    scm {
        url 'https://github.com/PaeDae/android-airship-adapter'
        connection 'https://github.com/PaeDae/android-airship-adapter.git'
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            artifact("$buildDir/outputs/aar/" + project.name + "-release.aar")
            artifact(sourcesJar)

            pom.withXml {
                def root = asNode()

                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://docs.gimbal.com')
                root.children().last() + pomConfig
            }
        }
    }
}